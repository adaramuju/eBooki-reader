apply plugin: 'com.android.application'

ext {
    codeNumber = Integer.parseInt(appCodeNumber)
    versionNumber = appVersionNumberBase + "." + appVersionNumberIndex
    db = Integer.parseInt(appDB)
    println ""
    println "Version: [" + versionNumber + " - " + codeNumber + "]"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "net.autogroup.ebooki"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode codeNumber
        versionName versionNumber
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false

    }
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOG", "true"
        }
        debug {
            buildConfigField "boolean", "LOG", "true"
        }
    }
    buildToolsVersion '28.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        // Configures multiple APKs based on ABI.
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk false
        }

    }


    flavorDimensions "version"
    productFlavors {
        beta {
            dimension "version"
            applicationId "net.autogroup.ebooki"
            manifestPlaceholders = [
                    appName       : "Beta " + versionNumber,
                    appIcon       : "@mipmap/icon_pdf_reader",
                    appRoundIcon  : "@mipmap/icon_pdf_reader",
                    appGdriveKey  : "",
                    admobAppId    : "ca-app-pub-8347903083053959~8769894873",
                    admobBannerId : "ca-app-pub-8347903083053959/4166397275",
                    admobFullId   : "ca-app-pub-8347903083053959/2769081274"
            ]
            buildConfigField "boolean", "IS_BETA", "true"
            buildConfigField "boolean", "IS_BETA_SEND_REPORTS", "true"
        }
        ebooki {
            dimension "version"
            applicationId "net.autogroup.ebooki"
            manifestPlaceholders = [
                    appName     : "EBooki",
                    appIcon     : "@mipmap/icon_pdf_reader",
                    appRoundIcon: "@mipmap/icon_pdf_reader",
                    appGdriveKey  : "",
                    admobAppId    : "ca-app-pub-8347903083053959~8769894873",
                    admobBannerId : "ca-app-pub-8347903083053959/4166397275",
                    admobFullId   : "ca-app-pub-8347903083053959/2769081274"
            ]
            buildConfigField "boolean", "IS_BETA", "false"
            buildConfigField "boolean", "IS_BETA_SEND_REPORTS", "false"
        }
        pro {
            dimension "version"
            applicationId "net.autogroup.ebooki.pro"
            manifestPlaceholders = [
                    appName     : "Librera PRO",
                    appIcon     : "@mipmap/icon_pdf_pro",
                    appRoundIcon: "@mipmap/icon_pdf_pro",
                    appGdriveKey  : "",
                    admobAppId    : "",
                    admobBannerId : "",
                    admobFullId   : ""
            ]
            buildConfigField "boolean", "IS_BETA", "false"
            buildConfigField "boolean", "IS_BETA_SEND_REPORTS", "false"
        }
    }


    def abiCodes = ['armeabi-v7a': 0, 'arm64-v8a': 1, 'x86': 2, 'x86_64': 3]

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavor = variant.productFlavors[0].name.capitalize()
            def abiName = getFilter("ABI")
            def code = abiCodes.get(abiName, 0) + variant.versionCode
            abiName = abiName.replace("arm64-v8a", "arm64").replace("armeabi-v7a", "arm")

            output.versionCodeOverride = code
            def fullName = "Librera ${flavor}-${versionNumber}-c${code}-d${db}-${abiName}.apk";
            output.outputFileName = fullName;

        }
    }


}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:multidex:1.0.3'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'com.android.support:appcompat-v7:28+'
    implementation 'com.android.support:recyclerview-v7:28+'
    implementation 'com.android.support:cardview-v7:28+'
    implementation 'com.android.support:support-v4:28+'
    implementation 'com.android.support:customtabs:28+'
    implementation 'com.getbase:floatingactionbutton:1.10.1'

    implementation 'org.greenrobot:eventbus:3.1.1'


    //implementation 'com.google.firebase:firebase-database:16.1.0'


    implementation ('org.greenrobot:greendao:3.2.2'){
        exclude group: 'org.greenrobot.greendao.rx'
    }
    implementation 'org.greenrobot:greendao-api:3.2.2'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.burgstaller:okhttp-digest:1.18'
    implementation 'com.squareup.okio:okio-parent:1.17.3'
    implementation 'com.github.joniles:rtfparserkit:1.13.0'



    implementation 'org.zwobble.mammoth:mammoth:1.4.0'
    implementation 'javax.xml.stream:stax-api:1.0-2'


    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    implementation 'com.google.http-client:google-http-client-gson:1.28.0'
    implementation('com.google.api-client:google-api-client-android:1.28.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev156-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    betaImplementation project(':pro')
    //betaImplementation 'com.google.firebase:firebase-ads:17+'
    //betaImplementation 'com.google.firebase:firebase-core:16+'


    ebookiImplementation 'com.google.firebase:firebase-ads:17+'
    betaImplementation 'com.google.firebase:firebase-ads:17+'
    ebookiImplementation 'com.google.firebase:firebase-core:16+'
    ebookiImplementation 'com.google.firebase:firebase-core:16+'

    proImplementation 'com.google.firebase:firebase-core:16+'
    proImplementation project(':pro')

    proImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    betaImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    ebookiImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
}


task copyApks(type: Copy) {
    if (project.hasProperty('beta')) {
        println "My Task: [copy apks]"

        delete fileTree("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing").include("*.apk")

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def flavor = variant.productFlavors[0].name
                println "build/outputs/apk/${flavor}/release/"
                from file(
                        "build/outputs/apk/${flavor}/release/"
                )
                include "*.apk"
                into file("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing/")
            }
        }


        File file = new File('/home/ivan-dev/Dropbox/FREE_PDF_APK/testing/version.txt')
        file.write versionNumber
    }
}

task incVersion() {
    doLast {


        Properties props = new Properties()
        File propsFile = new File('app/gradle.properties')
        props.load(propsFile.newDataInputStream())

        int appCodeNumber = Integer.parseInt(props.getProperty('appCodeNumber'))
        appCodeNumber += 4

        int appVersionNumberIndex = Integer.parseInt(props.getProperty('appVersionNumberIndex'))
        appVersionNumberIndex += 1

        props.setProperty('appCodeNumber', "" + appCodeNumber)
        props.setProperty('appVersionNumberIndex', "" + appVersionNumberIndex)

        props.store(propsFile.newWriter(), null)

        println "My Task: [incVersion]"
    }

}

apply plugin: 'com.google.gms.google-services'